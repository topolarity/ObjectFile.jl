using ObjectFile, Base.BinaryPlatforms
using Test

@testset "basic" begin
    # Test that an empty file is a magic mismatch
    mktempdir() do dir
        fpath = joinpath(dir, "empty")
        touch(fpath)
        @test_throws MagicMismatch readmeta(fpath) do ohs
            @test false
        end
    end
end

function check_magic_mismatch(path, HandleType)
    err = try
        readmeta(open(path, "r"), HandleType)
    catch err
        err
    end

    @test err isa MagicMismatch
    if HandleType === ELFHandle
        @test occursin(r"Magic Number 0x[a-f0-9]{8} does not match expected ELF magic number 0x7f454c46", repr(err))
    elseif HandleType === COFFHandle
        @test occursin(r"Magic Number 0x[a-f0-9]{8} does not match expected PE magic number 0x50450000", repr(err))
    elseif HandleType === MachOHandle
        @test occursin(r"Invalid Magic \(0x[a-f0-9]{8}\)\!", repr(err))
    else
        @assert false "unexpected handle type"
    end

    return nothing
end

function test_libfoo_and_fooifier(fooifier_path, libfoo_path)
    # Actually read it in
    oh_exe = only(readmeta(open(fooifier_path, "r")))
    oh_lib = only(readmeta(open(libfoo_path, "r")))

    # Tease out some information from the containing folder name
    dir_path = basename(dirname(libfoo_path))
    types = Dict(
        "linux" => ELFHandle,
        "mac" => MachOHandle,
        "win" => COFFHandle,
    )

    platform = dir_path[1:end-2]
    H = types[platform]
    bits = dir_path[end-1:end]

    platforms = Dict(
        "linux32" => Platform("i686", "linux"),
        "linux64" => Platform("x86_64", "linux"),
        "mac64" => Platform("x86_64", "macos"),
        "win32" => Platform("i686", "windows"),
        "win64" => Platform("x86_64", "windows"),
    )

    @testset "$(dir_path)" begin
        @testset "General Properties" begin
            for oh in (oh_exe, oh_lib)
                # Test that we got the right type
                @test typeof(oh) <: H

                # Test that the wrong types all error as expected
                for MismatchedType in (ELFHandle, COFFHandle, MachOHandle)
                    H === MismatchedType && continue
                    check_magic_mismatch(fooifier_path, MismatchedType)
                end

                # Test that we got the right number of bits
                @test is64bit(oh) == (bits == "64")
                @test platforms_match(Platform(oh), platforms[dir_path])

                # Everything is always little endian
                @test endianness(oh) == :LittleEndian
            end

            # None of these are .o files
            @test !isrelocatable(oh_exe)
            @test !isrelocatable(oh_lib)

            # Ensure these are the kinds of files we thought they were
            @test isexecutable(oh_exe)
            @test islibrary(oh_lib)
            @test isdynamic(oh_exe) && isdynamic(oh_lib)
        end


        @testset "Dynamic Linking" begin
            # Ensure that `dir_path` is one of the RPath entries
            rpath = RPath(oh_exe)
            can_paths = canonical_rpaths(rpath)
            @test abspath(dir_path * Base.Filesystem.path_separator) in can_paths

            # Ensure that `fooifier` is going to try to load `libfoo`:
            foo_libs = find_libraries(oh_exe)
            @test !isempty(foo_libs)
            @test abspath(libfoo_path) in values(foo_libs)
        end

        # Ensure that `foo()` is referenced in both, defined in `libfoo`, and
        # not defined in `fooifier`.  Also ensure that `_main` is defined in
        # `fooifier` and is not present in `libfoo`.
        @testset "Symbols" begin
            syms_exe = collect(Symbols(oh_exe))
            syms_lib = collect(Symbols(oh_lib))

            syms_names_exe = symbol_name.(syms_exe)
            syms_names_lib = symbol_name.(syms_lib)

            # ELF stores the symbol name as "foo", MachO stores it as "_foo"
            foo_sym_name = mangle_symbol_name(oh_exe, "foo")
            main_sym_name = mangle_symbol_name(oh_exe, "main")

            @test foo_sym_name in syms_names_exe
            @test foo_sym_name in syms_names_lib
            @test main_sym_name in syms_names_exe
            @test !(main_sym_name in syms_names_lib)

            foo_idx_exe = findfirst(syms_names_exe .== foo_sym_name)
            main_idx_exe = findfirst(syms_names_exe .== main_sym_name)
            foo_idx_lib = findfirst(syms_names_lib .== foo_sym_name)

            @test foo_idx_exe != 0
            @test main_idx_exe != 0
            @test foo_idx_lib != 0

            # definedness doesn't seem to be for COFF files...
            if !isa(oh_exe, COFFHandle)
                @test isundef(syms_exe[foo_idx_exe])
                @test !isundef(syms_exe[main_idx_exe])
                @test !isundef(syms_lib[foo_idx_lib])
            end

            @test !islocal(syms_exe[foo_idx_exe])
            @test !islocal(syms_exe[main_idx_exe])
            @test !islocal(syms_lib[foo_idx_lib])

            # COFF symbols are followed by auxiliary records, which are skipped
            if isa(oh_exe, COFFHandle)
                for (oh, syms) in ((oh_exe, syms_exe), (oh_lib, syms_lib))
                    naux = [ObjectFile.deref(sym).NumberOfAuxSymbols for sym in syms]
                    @test length(syms) + sum(naux) == header(oh).NumberOfSymbols
                    @test ObjectFile.symbol_number(syms[end]) + naux[end] == header(oh).NumberOfSymbols
                end
            end

            # Global detection doesn't seem to be working on OSX...
            if !isa(oh_exe, MachOHandle)
                @test isglobal(syms_exe[foo_idx_exe])
                @test isglobal(syms_exe[main_idx_exe])
                @test isglobal(syms_lib[foo_idx_lib])
            end

            if isa(oh_exe, ELFHandle)
                sections = Sections(oh_exe)
                dynsym = Symbols(only(findall(sections, ".dynsym")))
                symtab = Symbols(only(findall(sections, ".symtab")))

                @test section_name(Section(dynsym)) == ".dynsym"
                @test section_number(Section(dynsym)) != section_number(Section(symtab))
                @test section_number(Section(dynsym)) == section_number(Section(Symbols(dynsym[2])))
                @test symbol_number(dynsym[2]) == 2
                @test length(dynsym) <= length(symtab)
                @test symbol_name(dynsym[2]) == "_ITM_deregisterTMCloneTable"
            end
        end

        @testset "Printing" begin
            # Print out to an IOContext that will limit long lists
            io = IOContext(stdout, :limit => true)

            # Helper that shows the type, then the value:
            function tshow(x)
                type_name = typeof(x).name.name
                println(io, "INFO: Showing $(type_name)")
                show(io, x)
                print(io, "\n")
            end

            # Show printing of a Handle
            tshow(oh_lib)

            # Test showing of the header
            tshow(header(oh_lib))

            # Test showing of Sections
            sects = Sections(oh_exe)
            tshow(sects)
            tshow(sects[1])

            # Test showing of Segments on non-COFF
            if !isa(oh_exe, COFFHandle)
                segs = Segments(oh_lib)
                tshow(segs)
                tshow(segs[1])
            end

            # Test showing of Symbols
            syms = Symbols(oh_exe)
            tshow(syms)
            tshow(syms[1])

            # Test showing of RPath and DynamicLinks
            rpath = RPath(oh_exe)
            tshow(rpath)

            dls = DynamicLinks(oh_exe)
            tshow(dls)
            tshow(dls[1])
        end
    end
end

function test_fat_libfoo(file)
    ohs = readmeta(file isa IO ? file : open(file, "r"))
    @test isa(ohs, FatMachOHandle)
    @test length(ohs) == 2
    ntotal, n64 = 0, 0
    for oh in ohs
        ntotal += 1
        n64 += is64bit(oh)
    end
    @test ntotal == 2
    @test n64 == 1

    handles = collect(ohs)
    @test handles isa Vector{<:MachOHandle}
    @test length(handles) == 2
end

function test_metal(file)
    ohs = readmeta(open(file, "r"))
    @test isa(ohs, FatMachOHandle)
    @test length(ohs) == 2

    let oh = ohs[1]
        @test oh.header isa MachO.MachOHeader64
        @test findfirst(Sections(oh), "__TEXT,__compute") !== nothing
    end

    let oh = ohs[2]
        @test oh.header isa MachO.MetallibHeader
    end
end

# Run ELF tests
test_libfoo_and_fooifier("./linux32/fooifier", "./linux32/libfoo.so")
test_libfoo_and_fooifier("./linux64/fooifier", "./linux64/libfoo.so")

# Run MachO tests
test_libfoo_and_fooifier("./mac64/fooifier", "./mac64/libfoo.dylib")
test_fat_libfoo("./mac64/libfoo_fat.dylib")
test_fat_libfoo(IOBuffer(read("./mac64/libfoo_fat.dylib")))

# Rewrite the fat binary above with a `fat_header` using `FAT_MAGIC_64` (and so,
# 64-bit `fat_arch_64` entries), keeping its slices in place.
mktempdir() do dir
    data = read("./mac64/libfoo_fat.dylib")
    words(bytes) = ntoh.(reinterpret(UInt32, bytes))
    magic, nfat_arch = words(data[1:8])
    @test magic == 0xcafebabe
    header = UInt8[reinterpret(UInt8, hton.(UInt32[0xcafebabf, nfat_arch]))...]
    for i in 0:nfat_arch-1
        cputype, cpusubtype, offset, size, align = words(data[9 + 20i:28 + 20i])
        append!(header, reinterpret(UInt8, hton.(UInt32[cputype, cpusubtype])))
        append!(header, reinterpret(UInt8, hton.(UInt64[offset, size])))
        append!(header, reinterpret(UInt8, hton.(UInt32[align, 0])))
    end
    # The larger header must only overwrite the padding before the first slice
    @test all(iszero, data[9 + 20nfat_arch:length(header)])
    data[1:length(header)] = header

    fat64_path = joinpath(dir, "libfoo_fat64.dylib")
    write(fat64_path, data)
    test_fat_libfoo(fat64_path)
end
test_metal("./macmetal/dummy")

# Run COFF tests
test_libfoo_and_fooifier("./win32/fooifier.exe", "./win32/libfoo.dll")
test_libfoo_and_fooifier("./win64/fooifier.exe", "./win64/libfoo.dll")


# Ensure that ELF version stuff works
@testset "ELF Version Info Parsing" begin
    using ObjectFile.ELF

    # Assuming the version structs in the file are correct, test that we read
    # them correctly (and calculate hashes correctly).
    function check_verdef(v::ELF.ELFVersionEntry)
        @test v.ver_def.vd_version == 1
        @test v.ver_def.vd_cnt == length(v.names)
        if length(v.names) > 0
            @test v.ver_def.vd_hash == ELFHash(Vector{UInt8}(v.names[1]))
        end
    end
    function check_verneed(v::ELF.ELFVersionNeededEntry)
        @test v.ver_need.vn_version == 1
        @test v.ver_need.vn_cnt == length(v.auxes) == length(v.names)
        for i in 1:length(v.names)
            @test v.auxes[i].vna_hash == ELFHash(Vector{UInt8}(v.names[i]))
        end
    end

    libstdcxx_path = "./linux64/libstdc++.so.6"

    # Extract all pieces of `.gnu.version_d` from libstdc++.so, find the `GLIBCXX_*`
    # symbols, and use the maximum version of that to find the GLIBCXX ABI version number
    readmeta(libstdcxx_path) do ohs
        oh = only(ohs)
        verdef_symbols = unique(vcat((x -> x.names).(ELFVersionData(oh))...))
        verdef_symbols = filter(x -> startswith(x, "GLIBCXX_"), verdef_symbols)
        max_version = maximum([VersionNumber(split(v, "_")[2]) for v in verdef_symbols])
        @test max_version == v"3.4.25"
    end

    for p in ["./linux32/fooifier", "./linux32/libfoo.so",
              "./linux64/fooifier", "./linux64/libfoo.so",
              "./linux64/libstdc++.so.6"]
        readmeta(p) do ohs
            oh = only(ohs)
            foreach(check_verdef, ELFVersionData(oh))
            foreach(check_verneed, ELFVersionNeededData(oh))
        end
    end

end

# Ensure that these tricksy win32 files work
@testset "git win32 problems" begin
    # Test that 6a66694a8dd5ca85bd96fe6236f21d5b183e7de6 fix worked
    libmsobj_path = "./win32/msobj140.dll"

    dynamic_links = readmeta(libmsobj_path) do ohs
        oh = only(ohs)
        path.(DynamicLinks(oh))
    end

    @test "KERNEL32.dll" in dynamic_links
    @test "api-ms-win-crt-heap-l1-1-0.dll" in dynamic_links
    @test "api-ms-win-crt-convert-l1-1-0.dll" in dynamic_links
    @test "api-ms-win-crt-runtime-l1-1-0.dll" in dynamic_links

    whouses_exe = "./win32/WhoUses.exe"
    dynamic_links = readmeta(whouses_exe) do ohs
        oh = only(ohs)
        path.(DynamicLinks(oh))
    end

    @test "ADVAPI32.dll" in dynamic_links
    @test "KERNEL32.dll" in dynamic_links
    @test "libstdc++-6.dll" in dynamic_links
end

using Mmap
@testset "Finding dep_libs" begin
    function find_dep_libs(file)
        obj = only(readmeta(open(file, "r")))
        syms = collect(Symbols(obj))
        syms_names = symbol_name.(syms)
        sym = syms[findfirst(syms_names .== mangle_symbol_name(obj, "dep_libs"))]
        offset = symbol_offset(sym)
        filem = Mmap.mmap(file)
        data = String(filem[offset: (offset + 255)])
        @test contains(data, "libjulia-internal")
        @test contains(data, "libjulia-codegen")
        @test contains(data, "libopenlibm")
    end
    for file in readdir("./libjulias")
        find_dep_libs(joinpath("./libjulias", file))
    end
end

# Fixtures are generated by `./archives/build.sh`
@testset "Archives" begin
    member_names = ["foo.o", "bar_with_a_long_name.o"]

    @testset "$(file)" for (file, kind, H, names) in (
            ("libfoo_gnu.a", :gnu, ELFHandle, member_names),
            ("libfoo_gnu_nosymtab.a", :gnu, ELFHandle, member_names),
            ("libfoo_gnu64.a", :gnu64, ELFHandle, member_names),
            ("libfoo_darwin.a", :bsd, MachOHandle, member_names),
            ("libfoo_darwin64.a", :bsd64, MachOHandle, member_names),
            ("libfoo_bsd_nosymtab.a", :bsd, MachOHandle, member_names),
            # `llvm-lib` orders members by name
            ("foo.lib", :coff, COFFHandle, reverse(member_names)),
        )
        ah = readmeta(open(joinpath("./archives", file), "r"), ArchiveHandle)
        @test archive_kind(ah) == kind
        @test !isthin(ah)
        @test length(ah) == 2
        @test eltype(ah) == ArchiveMember
        # Symbol and name tables are not listed as members
        @test [m.name for m in ah] == names
        # An archive is an indexable collection of its members
        @test collect(ah) == [ah[i] for i in keys(ah)]
        @test (ah[begin], ah[end]) == (first(ah), last(collect(ah)))
        @test findfirst(m -> m.name == names[end], ah) == lastindex(ah)
        @test all(m.mode == 0o644 for m in ah)

        for member in ah
            data = read(ah, member)
            @test length(data) == member.size
            # Each member is an object file of the archive's platform...
            if H !== nothing
                oh = only(readmeta(ah, member))
                @test oh isa H
                @test isrelocatable(oh)
                syms = symbol_name.(Symbols(oh))
                fn = startswith(member.name, "foo") ? "foo" : "bar"
                @test mangle_symbol_name(oh, fn) in syms
                # `bar()` calls `foo()`, which another member defines
                foo_sym = only(sym for sym in Symbols(oh) if symbol_name(sym) == mangle_symbol_name(oh, "foo"))
                @test isundef(foo_sym) == (fn == "bar")
                # ... and reads the same as it would on its own
                @test oh.header == only(readmeta(IOBuffer(data))).header
            end
        end
    end

    @testset "thin archive" begin
        ah = readmeta(open("./archives/libfoo_thin.a", "r"), ArchiveHandle)
        @test isthin(ah)
        @test archive_kind(ah) == :gnu
        @test [m.name for m in ah] == member_names
        @test all(m.offset === nothing for m in ah)
        @test_throws ArgumentError read(ah, first(ah))
        @test_throws ArgumentError readmeta(ah, first(ah))
    end

    @testset "import library" begin
        # The members of an import library all name the DLL they import from
        ah = readmeta(open("./archives/libfoo.dll.a", "r"), ArchiveHandle)
        @test archive_kind(ah) == :gnu
        @test all(m.name == "foo.dll" for m in ah)
    end

    @testset "kinds and names" begin
        # Build an archive out of `name => data` members
        function ar(members...; magic = "!<arch>\n")
            io = IOBuffer()
            write(io, magic)
            for (name, data) in members
                write(io, rpad(name, 16), rpad("0", 12), rpad("0", 6), rpad("0", 6),
                          rpad("644", 8), rpad(string(sizeof(data)), 10), "`\n", data)
                isodd(sizeof(data)) && write(io, "\n")
            end
            return readmeta(IOBuffer(take!(io)), ArchiveHandle)
        end
        names(ah) = [m.name for m in ah]

        # An empty archive is the same in every kind
        @test archive_kind(ar()) == :gnu
        @test isempty(ar())
        # Only GNU (and COFF) member names are terminated by a "/"...
        @test archive_kind(ar("foo.o/" => "data")) == :gnu
        @test names(ar("foo.o/" => "data")) == ["foo.o"]
        @test archive_kind(ar("foo.o" => "data")) == :bsd
        @test names(ar("foo.o" => "data")) == ["foo.o"]
        # ... so a symbol table's name is only special in its own kind, and place
        @test names(ar("__.SYMDEF/" => "data")) == ["__.SYMDEF"]
        @test_throws ArgumentError ar("foo.o" => "data", "__.SYMDEF" => "data")
        @test_throws ArgumentError ar("foo.o/" => "data", "/" => "data")
        @test_throws ArgumentError ar("/" => "data", "foo.o/" => "data", "//" => "foo.o/\n")
        # GNU names must be terminated, and long names need a long name table
        @test_throws ArgumentError ar("/" => "data", "foo.o" => "data")
        @test_throws ArgumentError ar("/0" => "data")
        @test_throws ArgumentError ar("//" => "foo.o/\n", "/7" => "data")
        @test_throws ArgumentError ar("//" => "foo.o\n", "/0" => "data")
        @test names(ar("//" => "foo.o/\n", "/0" => "data")) == ["foo.o"]
        # Thin archives are always of the GNU kind
        @test_throws ArgumentError ar("foo.o" => ""; magic = "!<thin>\n")
    end

    @testset "not an archive" begin
        # Archives are only opened when asked for
        @test_throws MagicMismatch readmeta(open("./archives/libfoo_gnu.a", "r"))
        @test_throws MagicMismatch readmeta(open("./linux64/libfoo.so", "r"), ArchiveHandle)
        @test_throws MagicMismatch readmeta(IOBuffer(b"!<arc"), ArchiveHandle)
        # A truncated member header
        @test_throws EOFError readmeta(IOBuffer(b"!<arch>\nfoo.o/"), ArchiveHandle)
    end

    @testset "archive within a fat file" begin
        # A universal archive holds one archive per architecture; open a slice
        # by seeking to it.
        data = read("./archives/libfoo_darwin.a")
        offset = 4096
        fat = vcat(reinterpret(UInt8, hton.(UInt32[0xcafebabe, 1, 0x01000007, 3, offset, length(data), 12])),
                   zeros(UInt8, offset - 28), data)
        mktempdir() do dir
            fat_path = joinpath(dir, "libfoo_universal.a")
            write(fat_path, fat)
            open(fat_path, "r") do io
                fh = readmeta(io, FatMachOHandle)
                seek(io, only(fh.header.archs).offset)
                ah = readmeta(io, ArchiveHandle)
                @test [m.name for m in ah] == member_names
                @test only(readmeta(ah, first(ah))) isa MachOHandle
            end
        end
    end
end

# Fixtures are generated by `./coff/build.sh`
@testset "COFF objects" begin
    @testset "$(arch)" for (arch, platform) in (
            ("x86_64", Platform("x86_64", "windows")),
            ("i686", Platform("i686", "windows")),
            ("aarch64", Platform("aarch64", "windows")),
            ("thumbv7", Platform("armv7l", "windows")),
        )
        oh = only(readmeta(open("./coff/foo_$(arch).obj", "r")))
        @test oh isa COFFHandle
        @test platforms_match(Platform(oh), platform)
        @test is64bit(oh) == (arch in ("x86_64", "aarch64"))
        @test isrelocatable(oh)
        @test !isexecutable(oh)
        @test !islibrary(oh)
        @test isempty(DynamicLinks(oh))
        @test ".text" in section_name.(Sections(oh))
        foo_sym = only(sym for sym in Symbols(oh) if symbol_name(sym) == mangle_symbol_name(oh, "foo"))
        @test isglobal(foo_sym)
        @test !isundef(foo_sym)
    end

    @testset "not an object" begin
        not_obj(data) = readmeta(IOBuffer(data), COFFHandle)
        @test_throws MagicMismatch not_obj(b"hello, world, this is not an object file")
        # An x86_64 `Machine`, but with an optional header
        @test_throws MagicMismatch not_obj(vcat(b"d\x86", zeros(UInt8, 14), [0x08, 0x00], zeros(UInt8, 2)))
        # ... or with more sections than fit in the file
        @test_throws MagicMismatch not_obj(vcat(b"d\x86", [0xff, 0x00], zeros(UInt8, 16)))
        # An unknown `Machine`
        @test_throws MagicMismatch not_obj(vcat(b"zz", zeros(UInt8, 18)))
    end
end
